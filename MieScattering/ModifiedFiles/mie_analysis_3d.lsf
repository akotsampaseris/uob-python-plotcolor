# Start Analysis
run;

# Choose which of the 4 possible analyses you want to do
do_polar_plot = false;         # set false to not perform this test, true to perform the test
do_halfspace = false;         # set false to not perform this test, true to perform the test
do_cross_sections = true;     # set false to not perform this test, true to perform the test
do_field_enhancement = false; # set false to not perform this test, true to perform the test

# Define target wavelength used for polar plot and field enhancement when enabled:
target_wavelength = 0.5e-6;

# Define the resolution for the far field plots
polar_plot_res = 51;
halfspace_res = 31; # this number will significantly affect the time to run this analysis

# Far field polar plot and halfspace

setnamed("scat_ff", "target wavelength", target_wavelength);

if(do_polar_plot & do_halfspace ) {
    ?"  Calculating far field angular distribution and halfspace";
    ?"  NOTE: This calculation takes time!!";
    setnamed("scat_ff", "do polar plot",0);
    setnamed("scat_ff", "do halfspace",1);
    setnamed("scat_ff", "halfspace res", halfspace_res);
    setnamed("scat_ff", "polar plot res", polar_plot_res);
    runanalysis("scat_ff");
    ?"  Calculation done!";
}else{
    if(do_polar_plot) {
        ?"  Calculating far field angular distribution";
        setnamed("scat_ff", "do polar plot",0);
        setnamed("scat_ff", "do halfspace",0);
        setnamed("scat_ff", "halfspace res", halfspace_res);
        setnamed("scat_ff", "polar plot res", polar_plot_res);
        runanalysis("scat_ff");
    }
    if(do_halfspace) { # only for 3D simulation
        ?"  Calculating far field halfspace";
        ?"  NOTE: This calculation takes time!!";
        setnamed("scat_ff", "do polar plot",0);
        setnamed("scat_ff", "do halfspace",1);
        setnamed("scat_ff", "halfspace res", halfspace_res);
        setnamed("scat_ff", "polar plot res", polar_plot_res);
        runanalysis("scat_ff");
        ?"  Calculation done!";
    }
}
if(do_polar_plot) {
    XY = getresult("scat_ff","XY");
    f1 = getnamed("mie_source","frequency start");
    f2 = getnamed("mie_source","frequency stop");
    n2 = getfdtdindex("Au (Gold) - Johnson and Christy Copy 1",XY.f,f1,f2);
    n1 = getnamed("FDTD","background index");
    m = n2/n1;
    r = getnamed("sphere","radius"); # the radius of the mie particle
    size_parameter = 2*pi*r/XY.lambda * n1;
    phi = 90*pi/180;
    theta = XY.phi;
    S = mie3ds12(cos(theta),m,size_parameter);
    k = meshgridy(theta,2*pi/XY.lambda * n1);
    R = 1;
    fpoint = find(XY.lambda,target_wavelength);
    Etheta = exp(1i*k*R)/(-1i*k*R)*cos(phi)*S.S2;
    Ephi = exp(1i*k*R)/(1i*k*R)*sin(phi)*S.S1;
    polar(XY.phi,pinch(XY.E2,2,fpoint),pinch(abs(Etheta)^2+abs(Ephi)^2,2,fpoint),"","","XY");
    
    XZ = getresult("scat_ff","XZ");
    phi = XZ.phi+pi/2;
    theta = 90*pi/180;
    S = mie3ds12(cos(theta),m,size_parameter);
    k = meshgridy(phi,2*pi/XZ.lambda * n1);
    phi = meshgridx(phi,2*pi/XZ.lambda * n1);
    R = 1;
    Etheta = exp(1i*k*R)/(-1i*k*R)*cos(phi)*meshgridy(XZ.phi,S.S2);
    Ephi = exp(1i*k*R)/(1i*k*R)*sin(phi)*meshgridy(XZ.phi,S.S1);
    polar(XZ.phi,pinch(XZ.E2,2,fpoint),pinch(abs(Etheta)^2+abs(Ephi)^2,2,fpoint),"","","XZ");
    
    YZ = getresult("scat_ff","YZ");
    phi = 0;
    theta = YZ.phi;
    S = mie3ds12(cos(theta),m,size_parameter);
    k = meshgridy(theta,2*pi/YZ.lambda * n1);
    R = 1;
    Etheta = exp(1i*k*R)/(-1i*k*R)*cos(phi)*S.S2;
    Ephi = exp(1i*k*R)/(1i*k*R)*sin(phi)*S.S1;
    polar(YZ.phi,pinch(YZ.E2,2,fpoint),pinch(abs(Etheta)^2+abs(Ephi)^2,2,fpoint),"","","YZ");
}


# Cross section analysis
if(do_cross_sections) {
    ?"  Calculating scattering and absorption cross sections";
    
    # get sigma and particle radius
    sigmaabs = getresult("total","sigma");
    sigmascat = getresult("scat","sigma");
    r = getnamed("sphere","radius"); # the radius of the mie particle
    
    # Calculate cross-sections normalized to the particle area
    Qscat = sigmascat.sigma/(pi*r^2);
    Qabs  = -sigmaabs.sigma/(pi*r^2);
    lambda = sigmaabs.lambda;
    
    # calculate the size parameter
    size_parameter = 2*pi*r/lambda * n1;
    
    # compare with analytic based on material fit
    n2 = getfdtdindex("Au (Gold) - Johnson and Christy Copy 1",sigmaabs.f,min(sigmaabs.f),max(sigmaabs.f));
    n1 = getnamed("FDTD","background index");
    m = n2/n1;
    Qtheory = mie3d(m,size_parameter);
    
    # Plot results
    plot(size_parameter,Qscat,Qtheory.Qscat,"size parameter","Mie efficiency","Scattering cross section");
    legend("FDTD","Mie theory");
    plot(size_parameter,Qabs,Qtheory.Qabs, "size parameter","Mie efficiency","Absorption cross section");
    legend("FDTD","Mie theory");
}

# Field enhancement
if(do_field_enhancement) {
    ?"  Calculating field enhancement";
    
    E = getresult("x_normal_profile","E");
    E2 = E.E2; # returns |E|^2
    y = E.y;
    z = E.z;
    lambda = E.lambda;
    
    
    # choose one frequency point for the images
    fpoint = find(E.f, c/target_wavelength);
    
    # image the total field near the particle
    image(y*1e9,z*1e9,pinch(E2,4,fpoint),"y (nm)","z (nm)","|E|^2 in y-z plane at "+num2str(lambda(fpoint)*1e9)+" nm");
    ?"    Maximum field enhancement over all wavelengths (|E|^2/|Einc|^2) in y-z plane is: " + num2str(max(E2));
    
    
    E = getresult("y_normal_profile","E");
    E2 = E.E2; # returns |E|^2
    x = E.x;
    z = E.z;
    image(x*1e9,z*1e9,pinch(E2,4,fpoint),"x (nm)","z (nm)","|E|^2 in x-z plane at "+num2str(lambda(fpoint)*1e9)+" nm");
    ?"    Maximum field enhancement over all wavelengths (|E|^2/|Einc|^2) in x-z plane is: " + num2str(max(E2));
    
    
    E = getresult("z_normal_profile","E");
    E2 = E.E2; # returns |E|^2
    x = E.x;
    y = E.y;
    image(x*1e9,y*1e9,pinch(E2,4,fpoint),"x (nm)","y (nm)","|E|^2 in x-y plane at "+num2str(lambda(fpoint)*1e9)+" nm");
    ?"    Maximum field enhancement over all wavelengths (|E|^2/|Einc|^2) in x-y plane is: " + num2str(max(E2));
}

